plugins {
  id 'dev.clojurephant.clojure' version '0.7.0-alpha.1'
  id 'application'
  id 'maven-publish'
  id 'com.google.protobuf' version '0.8.18'
}

group = 'net.clojars.ekimber'
version = '0.15.0'

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org'
  }
}

dependencies {
    implementation 'org.clojure:clojure:1.11.1'
    implementation 'io.github.protojure:grpc-client:2.2.0'
    implementation 'org.clj-commons:byte-streams:0.3.1'

    testRuntimeOnly 'org.ajoberstar:jovial:0.3.0'

    devImplementation 'org.clojure:tools.namespace:1.3.0'
}

tasks.withType(Test) {
  useJUnitPlatform()
}

sourceSets {
    main {
        clojure {
            srcDir 'generated/main/clojure'
        }
    }
}

clojure {
    builds {
        test {
            aotNamespaces = []
        }
    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:3.7.1"
    }
    plugins {
        clojure {
            path = './bin/protoc-gen-clojure'
        }
    }
    generateProtoTasks {
       all().each { task ->
          task.builtins {
              remove java
          }
          task.plugins {
              clojure {
                  option 'grpc-client'
              }
          }
       }
    }
}


publishing {
  publications {
      main(MavenPublication) {
          from components.java
      }
  }
  repositories {
    maven {
      name = 'clojars'
      url = 'https://repo.clojars.org'
      credentials {
        username = System.env['CLOJARS_USER']
        password = System.env['CLOJARS_PASSWORD']
      }
    }
  }
}
